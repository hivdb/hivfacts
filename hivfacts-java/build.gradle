plugins {
	// Apply the java-library plugin to add support for Java Library
	id 'java-library'
	id 'jacoco'
	id 'maven-publish'
}

group = 'edu.stanford.hivdb.hivfacts'
version = '2022.11'

sourceCompatibility = 15
targetCompatibility = 15

description = 'Amino acid / codon classification data of HIV-1 pol'

dependencies {
	implementation 'org.apache.commons:commons-lang3:3.9'
	implementation 'commons-io:commons-io:2.6'
	implementation 'com.google.code.gson:gson:2.9.1'
	implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.codepoetics:protonpack:1.15'
	testImplementation 'junit:junit:4.12'
    implementation project(':sierra-core')
}

repositories {
	mavenCentral()
}

task copyData(type: Copy, group: 'build') {
	from '../data'
	into 'src/main/resources/'
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}

assemble.dependsOn copyData
test.dependsOn copyData

task sourcesJar(type: Jar) {
	dependsOn classes
	classifier 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}
